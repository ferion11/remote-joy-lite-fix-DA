static const float2 INPUT_SIZE = float2(480.0f, 272.0f);
static const float2 HALF_PIXEL = float2(0.5f, 0.5f);
static const float PI = 3.141592653589793238462643383f;
static const float EPS = 1e-6;

sampler2D textureSampler;
float2 outputSize;

float sinc(float x) {
	return sin(PI * x) / (PI * x);
}

float lanczos4(float x) {
	if (x < EPS) {
		return 1.0;
	}
	return sinc(x) * sinc(x * 0.25);
}

float4 rgb(float inputX, float inputY) {
	return tex2D(textureSampler, (float2(inputX, inputY) + HALF_PIXEL) / INPUT_SIZE);
}

float4 interpolateHorizontally(float2 inputPos, int2 inputPosFloor, int dy) {
	float sumOfWeights = 0.0f;
	float4 sumOfWeightedPixel = 0.0f;

	float x;
	float weight;

	x = inputPos.x - (inputPosFloor.x - 3);
	weight = lanczos4(x);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * rgb(inputPosFloor.x - 3, inputPosFloor.y + dy);

	--x;
	weight = lanczos4(x);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * rgb(inputPosFloor.x - 2, inputPosFloor.y + dy);

	--x;
	weight = lanczos4(x);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * rgb(inputPosFloor.x - 1, inputPosFloor.y + dy);

	--x;
	weight = lanczos4(x);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * rgb(inputPosFloor.x + 0, inputPosFloor.y + dy);

	x = 1.0f - x;
	weight = lanczos4(x);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * rgb(inputPosFloor.x + 1, inputPosFloor.y + dy);

	++x;
	weight = lanczos4(x);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * rgb(inputPosFloor.x + 2, inputPosFloor.y + dy);

	++x;
	weight = lanczos4(x);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * rgb(inputPosFloor.x + 3, inputPosFloor.y + dy);

	++x;
	weight = lanczos4(x);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * rgb(inputPosFloor.x + 4, inputPosFloor.y + dy);

	return sumOfWeightedPixel / sumOfWeights;
}

float4 process(float2 outputPos : VPOS) : COLOR0 {
	float2 inputPos = outputPos / outputSize * INPUT_SIZE;
	int2 inputPosFloor = (int2)inputPos;

	// Vertical interporation
	float sumOfWeights = 0.0;
	float4 sumOfWeightedPixel = 0.0;

	float weight;
	float y;

	y = inputPos.y - (inputPosFloor.y - 3);
	weight = lanczos4(y);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * interpolateHorizontally(inputPos, inputPosFloor, -3);

	--y;
	weight = lanczos4(y);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * interpolateHorizontally(inputPos, inputPosFloor, -2);

	--y;
	weight = lanczos4(y);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * interpolateHorizontally(inputPos, inputPosFloor, -1);

	--y;
	weight = lanczos4(y);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * interpolateHorizontally(inputPos, inputPosFloor, +0);

	y = 1.0f - y;
	weight = lanczos4(y);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * interpolateHorizontally(inputPos, inputPosFloor, +1);

	++y;
	weight = lanczos4(y);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * interpolateHorizontally(inputPos, inputPosFloor, +2);

	++y;
	weight = lanczos4(y);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * interpolateHorizontally(inputPos, inputPosFloor, +3);

	++y;
	weight = lanczos4(y);
	sumOfWeights += weight;
	sumOfWeightedPixel += weight * interpolateHorizontally(inputPos, inputPosFloor, +4);

	return float4((sumOfWeightedPixel / sumOfWeights).xyz, 1.0f);
}
